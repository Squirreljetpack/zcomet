#autoload

############################################################
# zcomet's plugin directories are dynamic named
# directories - an idea inspired by Marlon Richert's Znap.
#
# But zcomet's named directories are not necessarily the
# root directory of the Git repository -- they are often a
# subdirectory.
#
# For example, running
#
#   zcomet load ohmyzsh plugins/extract
#
# gives you an ~[extract] named directory that points to
#
#   /path/to/ohmyzsh/ohmyzsh/plugins/extract
#
# not to the root of the Oh-My-Zsh repository. That way you
# can have more than one named directory in repositories
# that contain multiple plugins.
############################################################

emulate -L zsh

typeset -ga reply
local -a dirs
local expl

if [[ $1 == 'n' ]]; then
	[[ $2 == 'zcomet-bin' ]] && reply=( ${ZCOMET[SCRIPT]:A:h} ) && return 0
  dirs=( ${(M)ZCOMET_NAMED_DIRS:#*/$2} )
  [[ -d ${dirs[1]} ]]	&& reply=( ${dirs[1]} ) && return 0
  return 1
elif [[ $1 == 'd' ]]; then
	if [[ $2 == ${ZCOMET[SCRIPT]:A:h} ]]; then
		reply=( 'zcomet-bin' ${#2} )
		return 0
  elif (( ${ZCOMET_NAMED_DIRS[(Ie)$2]} )); then
    reply=( ${2:t} ${#2} )
    return 0
  fi
  return 1
elif [[ $1 == 'c' ]]; then
  _tags named-directories
  _tags && _requested named-directories expl 'dynamic named directories' &&
    compadd ${expl} -S\] -- ${ZCOMET_NAMED_DIRS:t} 'zcomet-bin'
  return 1
fi

# vim: ft=zsh:ts=2:sts=2:sw=2
