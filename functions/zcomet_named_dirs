#autoload

############################################################
# zcomet's plugin directories are dynamic named
# directories - an idea inspired by Marlon Richert's Znap.
#
# But zcomet's named directories are not necessarily the
# root directory of the Git repository -- they are often a
# subdirectory.
#
# For example, running
#
#   zcomet load ohmyzsh plugins/extract
#
# gives you an ~[extract] named directory that points to
#
#   /path/to/ohmyzsh/ohmyzsh/plugins/extract
#
# not to the root of the Oh-My-Zsh repository. That way you
# can have more than one named directory in repositories
# that contain multiple plugins.
############################################################

typeset -ga reply
local -a dirs
local expl

if [[ $1 == 'n' ]]; then
  dirs=( ${(M)ZCOMET_NAMED_DIRS:#*/$2} )
	# TODO: What if more than one directory qualifies for the same directory
	# name?
  [[ -d ${dirs[@]:0:1} ]]	&& reply=( "${dirs[@]:0:1}" ) && return 0
  return 1
elif [[ $1 == 'd' ]]; then
  if (( ${ZCOMET_NAMED_DIRS[(Ie)$2]} )); then
    reply=( "${2:t}" "${#2}" )
    return 0
  fi
  return 1
elif [[ $1 == 'c' ]]; then
  # _description completions expl 'dynamic named directories'
  # compadd "${expl[@]}" -U -V zcomet -S\] -- "${ZCOMET_NAMED_DIRS[@]:t}" && return 0
  _tags named-directories
  _tags && _requested named-directories expl 'dynamic named directories' &&
    compadd "${expl[@]}" -S\] -- "${ZCOMET_NAMED_DIRS[@]:t}"
  return 1
fi

# vim: ft=zsh:ts=2:sts=2:sw=2